plugins {
	id "net.researchgate.release" version "3.0.2"
	id "com.diffplug.spotless" version "6.19.0"
	id "com.github.jakemarsden.git-hooks" version "0.0.2"
	id 'com.github.jk1.dependency-license-report' version '2.1'
	id "io.freefair.lombok" version "8.0.1"
	id 'org.springframework.boot' version '3.1.0'
}

apply plugin: "java"
apply plugin: 'io.spring.dependency-management'
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "application"
apply plugin: "jacoco"

sourceCompatibility = 17
targetCompatibility = 17

group = 'com.jyuzawa'
description="The most accurate personal race course tracker."

repositories {
	mavenCentral()
}

release {
	tagTemplate = 'v${version}'
}

lombok {
	version = "1.18.26"
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
}
test.finalizedBy jacocoTestReport


licenseReport {
	renderers = [new com.github.jk1.license.render.TextReportRenderer()]
}

spotless {
	ratchetFrom 'origin/master'
	java {
		target '**/*.java'
		licenseHeader '/*\n * Copyright (c) $YEAR James Yuzawa (https://www.jyuzawa.com/)\n * All rights reserved. Licensed under the MIT License.\n */'
		removeUnusedImports()
		importOrder()
		palantirJavaFormat()
		endWithNewline()
	}
	format 'misc', {
		target '**/*.gradle', '**/*.proto', '**/*.feature'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	format 'md', {
		target '**/*.md'
		trimTrailingWhitespace()
		endWithNewline()
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	testImplementation 'org.mockito:mockito-core:5.3.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
	testImplementation 'org.junit.platform:junit-platform-suite:1.9.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
}

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

mainClassName = 'com.jyuzawa.miles_per_awa.MilesPerAwa'

jar {
	doFirst {
		manifest {
			attributes(
				'Built-By'               : System.properties['user.name'],
				'Build-Timestamp'        : new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
				'Specification-Version'  : project.version,
				'Implementation-Version' : "${-> getGitHash()}",
				'Created-By'             : "Gradle ${gradle.gradleVersion}",
				'Build-Jdk'              : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
				'Build-OS'               : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
			)
		}
	}
}
jar.dependsOn(generateLicenseReport)

distributions {
	main {
		contents {
			from 'LICENSE'
			from 'README.md'
			from "${project.buildDir}/reports/dependency-license/THIRD-PARTY-NOTICES.txt"
		}
	}
}

gitHooks {
	hooks = ['pre-push': 'spotlessCheck']
}
